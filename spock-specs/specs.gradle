displayName = "Spock Framework - Specs for Core Module"

description = "Spock specifications for the Core Module. Yes, we eat our own dog food."

apply plugin: "spock-base"

configurations {
  spockReport
}

dependencies {
  groovy libs.junit // necessary to make @NotYetImplemented transform work (transform that ships with Groovy and statically references third-party class junit.framwork.AssertionFailedError)

  testCompile project(":spock-core")
  testRuntime libs.asm
  testRuntime libs.cglib
  testRuntime libs.objenesis
  testRuntime libs.h2database

  spockReport project(":spock-report")
}

ext.spockLogFileDir = file("$buildDir/spock/logFiles")
ext.spockLogFileName = "spock-log"

test {
  finalizedBy "spockReport"
  reports.junitXml.enabled = false
  reports.html.enabled = false

  testLogging.exceptionFormat = "full"
  testLogging.showExceptions = true
  maxParallelForks = 1

  systemProperty "spock.logFileDir", spockLogFileDir
  systemProperty "spock.logFileName", spockLogFileName
  //systemProperty "spock.reportServerAddress", "127.0.0.1"

  doFirst {
    delete spockLogFileDir
  }
}

task spockReport(type: org.spockframework.gradle.GenerateSpockReport) {
  reportName = "spock-specs"
  reportFileName = "spock-specs.html"
  debug = true
  local = true
  liveLogFiles = (1..10).collect { file("$spockLogFileDir/$spockLogFileName-$it") }
  spockReportClasspath = configurations.spockReport
  outputDirectory = file("$buildDir/spock/reports/")
}

def prj = project

//workaround until Gradle gets soft task dependencies
gradle.addListener(new TaskExecutionListener() {
  void beforeExecute(Task task) {}

  void afterExecute(Task task, TaskState state) {
    if (task.path == ":spock-specs:test" && state.executed) {
      prj.spockReport.execute()
    }
  }
})

task groovyConsole(type: JavaExec) {
  main = "groovy.ui.Console"
  classpath = sourceSets.test.runtimeClasspath
}
