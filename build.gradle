buildscript {
  repositories {
    maven { url "http://dl.bintray.com/content/aalmiray/asciidoctor" }
    jcenter()
  }

  dependencies {
    classpath "org.asciidoctor:asciidoctor-gradle-plugin:0.7.0"
  }
}

apply plugin: "base"
apply plugin: "asciidoctor"

description = "Spock Framework"

ext {
  libs = [
    ant: "org.apache.ant:ant:1.9.2", // use same version as Groovy 2.3
    asm: "org.ow2.asm:asm:5.0.1", // use same version as Groovy 2.3
    cglib: "cglib:cglib-nodep:3.1",
    easymock: "org.easymock:easymock:3.2",
    groovy: "org.codehaus.groovy:groovy-all:2.3.3",
    h2database: "com.h2database:h2:1.3.174",
    jmock: "org.jmock:jmock:2.6.0",
    jmock_junit4: "org.jmock:jmock-junit4:2.6.0",
    junit: "junit:junit:4.11",
    log4j: "log4j:log4j:1.2.17",
    mockito: "org.mockito:mockito-all:1.9.5",
    objenesis: "org.objenesis:objenesis:2.1"
  ]
  
  baseVersion = "1.0"
  groovyVersion = "2.3"
  snapshotVersion = true
}

allprojects {  
  ext.displayName = null
  
  group = "org.spockframework"
  version = "$baseVersion-groovy-$groovyVersion" + (snapshotVersion ? "-SNAPSHOT" : "")
  ext.groovyLessVersion = baseVersion + (snapshotVersion ? "-SNAPSHOT" : "")
  
  apply from: script("common")
  
  if (System.getProperty("java.version").startsWith("1.8")) {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

apply from: script("ide")

subprojects {
  apply plugin: "groovy"
  apply plugin: "signing"

  sourceCompatibility = 1.5

  repositories {
    mavenCentral()
    maven { url "https://oss.jfrog.org/oss-snapshot-local/" }
  }

  configurations {
    all*.exclude module: "junit-dep"
  }

  dependencies {
    compile(project.name == "spock-gradle" ? [] : libs.groovy)
  }

  signing {
    sign configurations.archives
  }

  signArchives {
    onlyIf { gradle.taskGraph.hasTask(uploadArchives) }
  }

  configureJavadoc(javadoc)
  configureGroovydoc(groovydoc)

  task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
  }

  artifacts {
    archives sourcesJar, javadocJar
  }
}

task javadoc(type: Javadoc) {
  title "Spock Framework API Documentation ($groovyLessVersion)"
  def docPath = snapshotVersion ? "latest" : baseVersion
  destinationDir file("build/javadoc/$docPath")
  source subprojects.javadoc.source
  classpath = files(subprojects.javadoc.classpath)
}

configureJavadoc(javadoc)

task groovydoc(type: Groovydoc) {
  docTitle "Spock Framework API Documentation ($groovyLessVersion)"
  windowTitle "Spock Framework API Documentation ($groovyLessVersion)"
  def docPath = snapshotVersion ? "latest" : baseVersion
  destinationDir file("build/groovydoc/$docPath")
  source subprojects.groovydoc.source
  classpath = files(subprojects.groovydoc.classpath)
  groovyClasspath = project(":spock-core").groovydoc.groovyClasspath
}

task apidoc(dependsOn: ["javadoc", "groovydoc"])

configureGroovydoc(groovydoc)

asciidoctor {
  sourceDir = file("docs")
  logDocuments = true
  options = [
    attributes: [
      "source-highlighter": "coderay"
    ]
  ]
}

File script(String name) {
  project.file("gradle/${name}.gradle")
}

def configureJavadoc(task) {
  configure(task) {
    include "spock/**"
    include "org/spockframework/mock/*"
    configure(options) {
      links "http://download.oracle.com/javase/1.5.0/docs/api/"
      links "http://groovy.codehaus.org/gapi/"
      links "http://junit-team.github.io/junit/javadoc/4.11/"
      links "http://hamcrest.org/JavaHamcrest/javadoc/1.3/"
    }
  }
}

def configureGroovydoc(task) {
  configure(task) {
    include "spock/**"
    include "org/spockframework/mock/*"
  }
}


